package com.achess.client.copyParserLexer;
import java.util.List;
import java_cup.runtime.Symbol;
import com.achess.client.error.ClientError;
import com.achess.client.copy.*;
import java.util.ArrayList;
import com.achess.client.result.Result;
parser code  {: 	
  
    public CopyParser(CopyLexer lexer){
        super(lexer);		
    }

    public void syntax_error(Symbol cur_token) {        
		List<Integer> tokens = expected_token_ids();
        int line = cur_token.left;
        int column = cur_token.right;
        String lexeme = symbl_name_from_id(cur_token.sym);
        String des = "Se esperaba:    \n";
        boolean count = false;
		for(Integer i : tokens) {	
            String fromId = symbl_name_from_id(i);                        
			des += fromId +" o ";                        
            count = true;
		}
        des = des.substring(0, des.length() - 3);

        ClientError.getError().log("COPY:\n");
		ClientError.getError().log("Error sintáctico ln:"+line+" col:"+column+ " " +lexeme + "\n");
		if(count) ClientError.getError().log(des+"\n");
	}

    public void report_fatal_error(String message, Object info) {
        ClientError.getError().log("COPY:\n");
		ClientError.getError().log("Message: " + message);
		ClientError.getError().log("Info: " + info.toString());		
	}
    
:};

terminal HTML_OPEN, H1_OPEN, H2_OPEN;
terminal FOR_OPEN; 
terminal TABLE_OPEN, TR_OPEN, TH_OPEN, TD_OPEN;
terminal HTML_CLOSE, H1_CLOSE, H2_CLOSE;
terminal FOR_CLOSE; 
terminal TABLE_CLOSE, TR_CLOSE, TH_CLOSE, TD_CLOSE;
terminal BR_OPEN;

terminal ITERADOR, HASTA;

terminal STRING, INTEGER;
terminal String INTEGER_LITERAL;
terminal String STRING_LITERAL;
terminal String IDENTIFIER;
terminal String TEXT;

terminal RESULT;
terminal String SCORE, CLASES, METODOS, VARIABLES, COMENTARIOS; 
terminal String TIPO, FUNCION ,NOMBRE, PARAMETROS, TEXTO_COMENTARIO;

terminal LPAREN, RPAREN;
terminal LBRACKET, RBRACKET;
terminal SEMICOLON, COLON;
terminal COMMA, DOT;

terminal EQ;
terminal PLUS, MINUS, MULT, DIV;

terminal DPESOS;
terminal GT;

non terminal initialState;

non terminal field_definitions_opt;
non terminal field_definitions;
non terminal field_definition;
non terminal ArrayList<CopyVariable> variable_declarators;
non terminal CopyVariable variable_declarator;
non terminal CopyVariable variable_assignment;
non terminal String name;
non terminal String reserved;

non terminal Value access_result;
non terminal Value access_score;
non terminal Value access_variables;
non terminal Value access_methods;
non terminal Value access_classes;
non terminal Value access_comments;

non terminal Value result_variables;
non terminal Integer class_variables;
non terminal Integer method_variables;
non terminal Integer var_variables;
non terminal id_call;


non terminal Integer integer_value;

non terminal Integer type;
non terminal Value values;

non terminal Operation operation, term, factor;

non terminal h1_tag, h1_body;
non terminal h2_tag, h2_body;
non terminal table_tag, table_body;
non terminal tr_tag, tr_body;
non terminal th_tag, th_body;
non terminal td_tag, td_body;

non terminal html_element_no_for_tag;

non terminal for_tag, for_body;
non terminal for_body_elements, for_body_element;
non terminal for_table_tag, for_table_body;
non terminal for_table_elements, for_table_element;
non terminal for_tr_tag, for_tr_body;
non terminal for_tr_elements, for_tr_element;

non terminal br_tag;
non terminal call_variable;

non terminal table_elemets, table_element;
non terminal tr_elements, tr_element;

non terminal values_html;
non terminal html_declarator, html_body;
non terminal html_element;
non terminal html_tag_open, html_tag_close;
non terminal h1_tag_open, h1_tag_close;
non terminal h2_tag_open, h2_tag_close;
non terminal table_tag_open, table_tag_close;
non terminal tr_tag_open, tr_tag_close;
non terminal th_tag_open, th_tag_close;
non terminal td_tag_open, td_tag_close;
non terminal for_tag_open, for_tag_close;
non terminal br_tag_open;


start with initialState;


initialState ::= field_definitions_opt html_declarator
                ;

field_definitions_opt ::= 
                        | field_definitions
                        ;

field_definitions ::= field_definition
                    | field_definitions field_definition
                    ;

field_definition ::= type:type variable_declarators:arr SEMICOLON
                {:
                    arr.forEach(v -> {
                            v.setType(type);
                            if(v.op == null){
                                ParseCopy.getCopy().addVariable(v);
                            }else{
                                ParseCopy.getCopy().addVariable(v, v.op);
                            }
                            
                        });
                :}
                   | variable_assignment:v SEMICOLON
                   {:
                        ParseCopy.getCopy().reAssignVariable(v.getName(), v.op);
                   :}
                   ;
                
name ::= IDENTIFIER:st
        {:
            RESULT=st;
        :}
        | reserved:st
        {:
            RESULT=st;
        :}
        ;
        

reserved ::= SCORE:st
        {:
            RESULT=st;
        :}
        | CLASES:st
        {:
            RESULT=st;
        :}
        | METODOS:st
        {:
            RESULT=st;
        :}
        | VARIABLES:st
        {:
            RESULT=st;
        :}
        | COMENTARIOS:st
        {:
            RESULT=st;
        :}
        | TIPO:st
        {:
            RESULT=st;
        :}
        | FUNCION:st
        {:
            RESULT=st;
        :}
        | NOMBRE:st
        {:
            RESULT=st;
        :}
        | PARAMETROS:st
        {:
            RESULT=st;
        :}
        | TEXTO_COMENTARIO:st
        {:
            RESULT=st;
        :}
        ;


variable_assignment ::= name:name EQ operation:op
                        {:
                            CopyVariable v = new CopyVariable(name, 0, op);
                            RESULT=v;
                        :}
                        ;

variable_declarators ::= variable_declarator:v
                        {:
                            ArrayList<CopyVariable> arr = new ArrayList();
                            arr.add(v);
                            RESULT=arr;
                        :}
                        | variable_declarators:arr COMMA variable_declarator:v
                        {:                            
                            arr.add(v);
                            RESULT=arr;
                        :}
                        ;

variable_declarator ::= name:name
                        {:
                            CopyVariable v = new CopyVariable(name, 0);
                            RESULT=v;
                        :}
                    | variable_assignment:v
                    {:                        
                        RESULT=v;
                    :}
                    ;

integer_value ::= name:st
            {:
                CopyVariable v = ParseCopy.getCopy().getVariable(st);
                RESULT=null;  
                if(v == null) ClientError.getError().log("COPY\nLa variable " + st + " no existe ln:"+ stleft);
                else if(v.getValue() == null) ClientError.getError().log("COPY\n" + st + " No tiene ningún valor ln:"+ stleft);
                else if(v.getValue().getType() != Value.INTEGER){
                    ClientError.getError().log("COPY\n" + st + " debe ser entero ln:"+ stleft);                    
                }
                else{
                    RESULT=v.getValue().getIntegerValue();
                }
            :}
            | INTEGER_LITERAL:st
            {:
                RESULT=Integer.valueOf(st);
            :}
            ;

access_result ::= RESULT DOT result_variables:val
                {:
                    if(val == null){
                        val = new Value("", Value.ERROR);
                    }
                    RESULT=val;
                :}
                ;
result_variables ::= access_score:val
                {:
                    RESULT=val;
                :}
                | access_classes:val
                {:
                    RESULT=val;
                :}
                | access_methods:val
                {:
                    RESULT=val;
                :}
                | access_variables:val
                {:
                    RESULT=val;
                :}
                | access_comments:val
                {:
                    RESULT=val;
                :}
                ;


access_score ::= SCORE
                {:
                    RESULT=new Value(Result.getResult().getScore(), Value.STRING);
                :}
                ;

access_classes ::= CLASES LBRACKET integer_value:index RBRACKET DOT class_variables
                    {:
                        if(index != null){
                            RESULT=null;
                            String val = Result.getResult().getClasses(index);
                            if(val != null) RESULT=new Value(val , Value.STRING);  
                            else{
                                ClientError.getError().log("COPY\n"+"Valor fuera de los límites ln:"+ indexleft);
                            }                          
                        }
                    :}
                    ;

class_variables ::= NOMBRE
                    {:
                        RESULT = 0;
                    :}
                    ;

access_methods ::= METODOS LBRACKET integer_value:index RBRACKET DOT method_variables:param
                    {:
                        if(index != null){
                            RESULT=null;
                            String val = Result.getResult().getMethods(index, param);
                            if(val != null){
                                if(param == 2){
                                    RESULT=new Value(val , Value.INTEGER); 
                                }else{
                                    RESULT=new Value(val , Value.STRING); 
                                }                            
                            } 
                            else{
                                ClientError.getError().log("COPY\n"+"Valor fuera de los límites ln:"+ indexleft);
                            }    
                        }
                    :}
                    ;

method_variables ::= class_variables:n
                    {: 
                        RESULT=n;
                    :}
                    | TIPO
                    {: 
                        RESULT=1;
                    :}
                    | PARAMETROS
                    {: 
                        RESULT=2;
                    :}
                    ; 

access_variables ::= VARIABLES LBRACKET integer_value:index RBRACKET DOT var_variables:param
                    {:
                        if(index != null){
                            RESULT=null;
                            String val = Result.getResult().getVariables(index, param);
                            if(val != null) RESULT=new Value(val , Value.INTEGER); 
                            else{
                                ClientError.getError().log("COPY\n"+"Valor fuera de los límites ln:"+ indexleft);
                            }    
                        }
                    :}
                    ;

var_variables ::= class_variables:n
                    {:
                        RESULT=n;
                    :}
                    | TIPO
                    {: 
                        RESULT=1;
                    :}
                    | FUNCION
                    {:
                        RESULT=2; 
                    :}
                    ;

access_comments ::= COMENTARIOS LBRACKET integer_value:index RBRACKET DOT TEXTO_COMENTARIO
                    {:
                        if(index != null){
                            RESULT=null;
                            String val = Result.getResult().getComments(index);
                            if(val != null) RESULT=new Value(val , Value.STRING);  
                            else{
                                ClientError.getError().log("COPY\n"+"Valor fuera de los límites ln:"+ indexleft);
                            }                          
                        }
                    :}
                    ;

type ::= STRING
        {:
                RESULT=Value.STRING;
        :}
        | INTEGER
        {:
                RESULT=Value.INTEGER;
        :}
        ;

values ::= STRING_LITERAL:st
        {:
            RESULT=new Value(st, Value.STRING);
        :}
        | access_result:val
        {:
            RESULT=val;
        :}
        | INTEGER_LITERAL:st
        {:
            RESULT=new Value(st, Value.INTEGER);
        :}
        ;

operation ::= operation:opL PLUS:s term:opR
            {:
                RESULT=new Operation(Operation.SUMA, opL, opR, sleft);
            :}
            |operation:opL MINUS:s term:opR
            {:
                RESULT=new Operation(Operation.RESTA, opL, opR, sleft);
            :}
            |term:term
            {:
                RESULT=term;
            :}
            ;

term::= term:opL MULT:s factor:opR
            {:
                RESULT=new Operation(Operation.MULTIPLICACION, opL, opR, sleft);
            :}
        |term:opL DIV:s factor:opR
        {:
            RESULT=new Operation(Operation.DIVISION, opL, opR, sleft);
        :}
        |factor:factor    
        {:
            RESULT=factor;
        :}
        ;

factor::= values:val
        {:
            RESULT = new Operation(val, valleft);
        :}      
        | name:name
        {:
            RESULT = new Operation(name, nameleft);
        :}                         
        |LPAREN operation:op RPAREN      
        {:
            RESULT=op;
        :}                  
        | MINUS:s factor:factor
        {:
            RESULT=new Operation(Operation.NEGACION, factor, null, sleft);
        :}        
        ;

html_tag_open::= HTML_OPEN GT;
html_tag_close ::= HTML_CLOSE GT;
h1_tag_open::= H1_OPEN GT;
h1_tag_close ::= H1_CLOSE GT;
h2_tag_open::= H2_OPEN GT;
h2_tag_close ::= H2_CLOSE GT;
table_tag_open ::= TABLE_OPEN GT;
table_tag_close ::= TABLE_CLOSE GT;
tr_tag_open ::= TR_OPEN GT;
tr_tag_close ::= TR_CLOSE GT;
th_tag_open ::= TH_OPEN GT;
th_tag_close ::= TH_CLOSE GT;
td_tag_open ::= TD_OPEN GT;
td_tag_close ::= TD_CLOSE GT;
for_tag_open ::= FOR_OPEN ITERADOR COLON integer_value:index HASTA COLON integer_value:index2 GT;
for_tag_close ::= FOR_CLOSE GT;
br_tag_open ::= BR_OPEN GT
            ;

html_declarator ::=  html_tag_open html_body html_tag_close
                    ;

values_html ::= name
        | STRING_LITERAL 
        | INTEGER_LITERAL
        | TEXT
        | call_variable
        ;

call_variable ::= DPESOS LPAREN id_call RPAREN DPESOS;

id_call ::= name
        | access_result
        ;

html_body ::= html_element
            | html_body html_element;

html_element ::= html_element_no_for_tag
                | for_tag
                | values_html
                    ;

html_element_no_for_tag ::= h1_tag
                    | h2_tag
                    | table_tag                    
                    | br_tag
                    ;
            
h1_tag ::= h1_tag_open h1_body h1_tag_close;

h1_body ::= values_html
            ;

h2_tag ::= h2_tag_open h2_body h2_tag_close
            ;

h2_body ::= values_html
            ;

br_tag ::= br_tag_open;

for_tag::= for_tag_open for_body for_tag_close;

for_body ::= for_body_elements
            ;

for_body_elements ::= for_body_element
                    | for_body_elements for_body_element
                    ;

for_body_element ::= html_element_no_for_tag
                    | values_html
                    ;

table_tag ::= table_tag_open table_body table_tag_close
            ;            

table_body::= table_elemets
            ;

table_elemets ::= table_element
            | table_elemets table_element
            ;

table_element ::= tr_tag
                | for_table_tag
                ;

for_table_tag ::= for_tag_open for_table_body for_tag_close
                    ;

for_table_body ::= for_table_elements
                    ;

for_table_elements ::= for_table_element
                    |   for_table_elements for_table_element
                    ;

for_table_element ::= tr_tag
                    ;

tr_tag ::= tr_tag_open tr_body tr_tag_close
            ;

tr_body ::= tr_elements
        ;

tr_elements ::= tr_element
            | tr_elements tr_element
            ;

tr_element ::= th_tag
            | td_tag
            | for_tr_tag            
            ;

for_tr_tag ::= for_tag_open for_tr_body for_tag_close            
            ;

for_tr_body ::= for_tr_elements
                ;

for_tr_elements ::= for_tr_element
                |   for_tr_elements for_tr_element
                ;

for_tr_element ::= td_tag
                |  th_tag
                ;

th_tag ::= th_tag_open th_body th_tag_close
            ;

th_body ::= values_html
            ;

td_tag ::= td_tag_open td_body td_tag_close
            ;

td_body ::= values_html
            ;
            