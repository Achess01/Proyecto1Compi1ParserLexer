package com.achess.client.jsonParserLexer;
import java.util.List;
import java_cup.runtime.Symbol;
import com.achess.client.attribute.*;
import com.achess.client.jsonMembers.Comment;
import com.achess.client.jsonMembers.JavaClass;
import com.achess.client.jsonMembers.Method;
import com.achess.client.jsonMembers.Score;
import com.achess.client.jsonMembers.Variable;
import com.achess.client.error.Error;
import java.util.ArrayList;

parser code  {: 	
  
    public JsonParser(JsonLexer lexer){
        super(lexer);		
    }

    public void syntax_error(Symbol cur_token) {        
		List<Integer> tokens = expected_token_ids();
        int line = cur_token.left;
        int column = cur_token.right;
        String lexeme = symbl_name_from_id(cur_token.sym);
        String des = "Se esperaba:    \n";
        boolean count = false;
		for(Integer i : tokens) {	
            String fromId = symbl_name_from_id(i);                        
			des += fromId +" o ";                        
            count = true;
		}
        des = des.substring(0, des.length() - 3);

		Error.getError().log("Error sint√°ctico ln:"+line+" col:"+column+ " " +lexeme + "\n");		
		if(count) Error.getError().log(des+"\n");
	}

    public void report_fatal_error(String message, Object info) {
		Error.getError().log("Message: " + message);
		Error.getError().log("Info: " + info.toString());		
	}
    
:};

terminal LBRACE, RBRACE;
terminal LBRACKET, RBRACKET;
terminal COMMA, COLON;
//keys
terminal CLASSES_LITERAL, METHODS_LITERAL, VARIABLES_LITERAL, COMMENTS_LITERAL;
terminal SCORE_LITERAL;
terminal NAME_LITERAL, FUNCTION_LITERAL, TYPE_LITERAL, PARAMETERS_LITERAL;
terminal TEXT_LITERAL;
//values
terminal Integer INTEGER_LITERAL;
terminal String STRING_LITERAL;


non terminal initialState;
non terminal json_declaration;

non terminal ArrayList<Attribute> json_body;
non terminal Attribute json_body_item;

non terminal Attribute score_declaration;
non terminal Attribute variables_declaration;
non terminal Attribute methods_declaration;
non terminal Attribute classes_declaration;
non terminal Attribute comments_declaration;

non terminal ArrayList<Variable> list_variables_opt;
non terminal ArrayList<Variable> list_variables;
non terminal Variable variable_item;
non terminal Variable variable_item_body;

non terminal ArrayList<Method> list_methods_opt;
non terminal ArrayList<Method> list_methods;
non terminal Method method_item;
non terminal Method method_item_body;

non terminal ArrayList<JavaClass> list_classes_opt;
non terminal ArrayList<JavaClass> list_classes;
non terminal JavaClass class_item;
non terminal JavaClass class_item_body;

non terminal ArrayList<Comment> list_comments_opt;
non terminal ArrayList<Comment> list_comments;
non terminal Comment comment_item;
non terminal Comment comment_item_body;


non terminal String name_item;
non terminal String type_item;
non terminal String function_item;
non terminal Integer parameters_item;
non terminal String text_item;


start with initialState;


initialState ::= json_declaration
                ;

json_declaration ::= LBRACE json_body:body RBRACE
                    {:
                        if(!Error.getError().isErrorFounded()){
                            ValidateJson.validate(body);
                        }
                    :}
                    ;

json_body ::= json_body_item:attr
            {:
                ArrayList<Attribute> attributes = new ArrayList();
                attributes.add(attr);
                RESULT=attributes;
            :}
            | json_body:arr COMMA json_body_item:attr
            {:
                arr.add(attr);
                RESULT=arr;
            :}
            | error json_body_item
            | error RBRACKET        
            ;

json_body_item ::=  score_declaration:attr
                {:
                    RESULT=attr;
                :}
                |   variables_declaration:attr
                {:
                    RESULT=attr;
                :}
                |   methods_declaration:attr
                {:
                    RESULT=attr;
                :}
                |   classes_declaration:attr
                {:
                    RESULT=attr;
                :}
                |   comments_declaration:attr           
                {:
                    RESULT=attr;
                :}
                ;

score_declaration ::=   SCORE_LITERAL COLON STRING_LITERAL:score
                        {:
                            Attribute<Score> att = new Attribute(new Score(score), Attribute.SCORE, scoreleft);
                            RESULT=att;
                        :}
                        ;

variables_declaration ::=   VARIABLES_LITERAL COLON LBRACKET list_variables_opt:list RBRACKET
                            {:
                                ArrayList<Variable> arr;
                                if(list == null){
                                    arr = new ArrayList();
                                }else{
                                    arr = list;
                                }
                                Attribute<ArrayList<Variable>> att = new Attribute(arr, Attribute.VARIABLES, listleft);
                                RESULT=att;
                            :}
                            ;

list_variables_opt ::=  
                    |   list_variables:list
                    {:
                        RESULT=list;
                    :}
                    ;

list_variables ::=  variable_item:variable
                {:
                    ArrayList<Variable> arr = new ArrayList();
                    arr.add(variable);
                    RESULT=arr;
                :}
                |   list_variables:arr COMMA variable_item:variable
                {:
                    arr.add(variable);                    
                    RESULT=arr;
                :}
                ;

variable_item ::=   LBRACE variable_item_body:variable RBRACE
                    {:
                        RESULT=variable;
                    :}
                    ;

variable_item_body ::=  name_item:name COMMA type_item:type COMMA function_item:function
                        {:
                            Variable variable = new Variable(name, type, function);
                            RESULT=variable;
                        :}
                        ;                        
                        //Maybe add more productions to allow diferent orders

methods_declaration ::=   METHODS_LITERAL COLON LBRACKET list_methods_opt:list RBRACKET
                             {:
                                ArrayList<Method> arr;
                                if(list == null){
                                    arr = new ArrayList();
                                }else{
                                    arr = list;
                                }
                                Attribute<ArrayList<Method>> att = new Attribute(arr, Attribute.METHODS, listleft);
                                RESULT=att;
                            :}
                            ;

list_methods_opt ::=  
                    |   list_methods:list
                    {:
                        RESULT=list;
                    :}
                    ;

list_methods ::=  method_item:method
                {:
                    ArrayList<Method> arr= new ArrayList();
                    arr.add(method);
                    RESULT=arr;
                :}
                |   list_methods:arr COMMA method_item:method
                {:
                    arr.add(method);
                    RESULT=arr;
                :}
                ;

method_item ::=   LBRACE method_item_body:method RBRACE
                    {:
                        RESULT=method;
                    :}
                    ;

method_item_body ::=  name_item:name COMMA type_item:type COMMA parameters_item:par
                        {:
                            Method method = new Method(name, type, par);
                            RESULT=method;
                        :}
                        ;                        
                        //Maybe add more productions to allow diferent orders     

classes_declaration ::=   CLASSES_LITERAL COLON LBRACKET list_classes_opt:list RBRACKET
                            {:
                                ArrayList<JavaClass> arr;
                                if(list == null){
                                    arr = new ArrayList();
                                }else{
                                    arr = list;
                                }
                                Attribute<ArrayList<JavaClass>> att = new Attribute(arr, Attribute.CLASSES, listleft);
                                RESULT=att;
                            :}
                            ;

list_classes_opt ::=  
                    |   list_classes:list
                    {:
                        RESULT=list;
                    :}
                    ;

list_classes ::=  class_item:java_class
                {:
                    ArrayList<JavaClass> arr = new ArrayList();
                    arr.add(java_class);
                    RESULT=arr;
                :}
                |   list_classes:arr COMMA class_item:java_class
                {:
                    arr.add(java_class);
                    RESULT=arr;
                :}
                ;

class_item ::=   LBRACE class_item_body:java_class RBRACE
                    {:
                        RESULT=java_class;
                    :}
                    ;

class_item_body ::=  name_item:name
                        {:
                            JavaClass jc = new JavaClass(name);
                            RESULT = jc;
                        :}
                        ;                        
                        //Maybe add more productions to allow diferent orders     
comments_declaration ::=   COMMENTS_LITERAL COLON LBRACKET list_comments_opt:list RBRACKET
                        {:
                                ArrayList<Comment> arr;
                                if(list == null){
                                    arr = new ArrayList();
                                }else{
                                    arr = list;
                                }
                                Attribute<ArrayList<Comment>> att = new Attribute(arr, Attribute.COMMENTS, listleft);
                                RESULT=att;
                        :}                            
                        ;

list_comments_opt ::=  
                    |   list_comments:list
                    {:
                        RESULT=list;
                    :}
                    ;

list_comments ::=  comment_item:comment
                {:
                    ArrayList<Comment> arr = new ArrayList();
                    arr.add(comment);
                    RESULT=arr;
                :}
                |   list_comments:arr COMMA comment_item:comment
                {:
                    arr.add(comment);
                    RESULT=arr;
                :}
                ;

comment_item ::=   LBRACE comment_item_body:comment RBRACE
                    {:
                        RESULT=comment;
                    :}
                    ;

comment_item_body ::=  text_item:text
                        {:
                            Comment comment = new Comment(text);
                            RESULT=comment;
                        :}
                        ;                        
                        //Maybe add more productions to allow diferent orders                             

name_item ::=   NAME_LITERAL COLON STRING_LITERAL:st
                {:
                    RESULT=st;
                :}
                ;

type_item ::=   TYPE_LITERAL COLON STRING_LITERAL:st
                {:
                    RESULT=st;
                :}
                ;

function_item ::=   FUNCTION_LITERAL COLON STRING_LITERAL:st
                {:
                    RESULT=st;
                :}
                    ;

parameters_item ::= PARAMETERS_LITERAL COLON INTEGER_LITERAL:int_literal
                {:
                    RESULT=int_literal;
                :}
                    ;

text_item ::=   TEXT_LITERAL COLON STRING_LITERAL:st
                {:
                    RESULT=st;
                :}
                ;

